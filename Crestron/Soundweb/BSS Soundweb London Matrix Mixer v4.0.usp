/*******************************************************************************************
  SIMPL+ Module Information
  (Fill in comments below)
*******************************************************************************************/
/*
Dealer Name:	Crestron Electronics
System Name:
System Number:
Programmer:		PAC
Comments:
*/
/*******************************************************************************************
  Compiler Directives
  (Uncomment and declare compiler directives as needed)
*******************************************************************************************/
#SYMBOL_NAME "BSS Soundweb London Matrix Mixer v4.0"
// #HINT ""
#DEFINE_CONSTANT MAXOUTPUT   48
#DEFINE_CONSTANT MAXOUTPUTx2 96
// #CATEGORY "" 
#DEFAULT_VOLATILE
// #DEFAULT_NONVOLATILE
#ENABLE_STACK_CHECKING

#HELP_BEGIN
Only to be used with the "BSS Soundweb Matrix Mixer.umc" module
#HELP_END
/*******************************************************************************************
  DIGITAL, ANALOG and SERIAL INPUTS and OUTPUTS
  (Uncomment and declare inputs and outputs as needed)
*******************************************************************************************/
DIGITAL_INPUT subscribe$;
DIGITAL_INPUT onOff$[MAXOUTPUTx2];
ANALOG_INPUT input$, iMAXOUTPUT;
STRING_INPUT objectID$[3];
BUFFER_INPUT rx$[400];
ANALOG_INPUT gain$[MAXOUTPUT];

DIGITAL_OUTPUT onOff_fb$[MAXOUTPUTx2];
STRING_OUTPUT tx$;
ANALOG_OUTPUT gain_fb$[MAXOUTPUT];
/*******************************************************************************************
  Global Variables
  (Uncomment and declare global variables as needed)
  Note:  Be sure to initialize all declared STRING variables as needed
         For example, in Function Main: myString = "";
*******************************************************************************************/
INTEGER i,volumeOutput[MAXOUTPUT];
STRING returnString[4];
INTEGER returnI,subscribe;
INTEGER xOK;
STRING tempString[40];
INTEGER stateVarOnOff, stateVarGain, stateVarSub, stateVarReceive, x;
/*******************************************************************************************
  Functions
  (Add any additional functions here)
  Note:  Functions must be physically placed before the location in
         the code that calls them.
*******************************************************************************************/
String_Function iToVolumePercent(INTEGER int)
{                 
	if (int MOD 2 = 0)
	{                   
		returnString = "\x00" + chr(int/2) + "\x00\x00";
	}
	else 
	{
		returnString = "\x00" + chr(int/2) + "\x80\x00";
	} 	
    Return (returnString);
}

Integer_Function volumePercentToI(STRING str)
{                     
	if (!(mid(str, 3, 1) = "\x00"))
	{                   
		returnI = (byte(str, 2)*2)+1;
	}
	else 
	{
		returnI = byte(str, 2)*2;
	} 	
    Return (returnI);
}
/*******************************************************************************************
  Event Handlers
  (Uncomment and declare additional event handlers as needed)
*******************************************************************************************/
PUSH onOff$
{                                  
	stateVarOnOff = getLastModifiedArrayIndex();
	if (stateVarOnOff <= 48)
	{   
		stateVarOnOff = ((stateVarOnOff-1)*128) + (input$-1);
		makeString(tx$, "\x88\x00\x00\x03%s%s%s\x00\x00\x00\x01\x03\x03\x03\x03\x03", objectID$, chr(high(stateVarOnOff)), chr(low(stateVarOnOff)));
   	if(subscribe)
		{
  		makeString(tx$, "\x89\x00\x00\x03%s%s%s\x00\x00\x00\x00\x03\x03\x03\x03\x03", objectID$, chr(high(stateVarOnOff)), chr(low(stateVarOnOff)));  //ON OFF
			processlogic();  	
		}
	}
	else
	{
		stateVarOnOff = ((stateVarOnOff-49)*128) + (input$-1);
		makeString(tx$, "\x88\x00\x00\x03%s%s%s\x00\x00\x00\x00\x03\x03\x03\x03\x03", objectID$, chr(high(stateVarOnOff)), chr(low(stateVarOnOff)));
   	if(subscribe)
		{
  		makeString(tx$, "\x89\x00\x00\x03%s%s%s\x00\x00\x00\x00\x03\x03\x03\x03\x03", objectID$, chr(high(stateVarOnOff)), chr(low(stateVarOnOff)));  //ON OFF
			processlogic();  	
		}
	}
}

CHANGE gain$
{                          
	x = getLastModifiedArrayIndex();
	if(volumeOutput[x] <> gain$[x])
	{
		stateVarGain = 16384 + (((x-1)*128) + (input$-1));
		volumeOutput[x] = gain$[x];
		gain_fb$[x] = gain$[x];
		makeString(tx$, "\x8D\x00\x00\x03%s%s%s%s\x03\x03\x03\x03\x03", objectID$, chr(high(stateVarGain)), chr(low(stateVarGain)), iToVolumePercent(gain$[x]));
	}
}
  
PUSH subscribe$
{              
	wait(20)
	{
		stateVarSub = input$-1;
		for (i = 1 to iMAXOUTPUT)
		{   
			stateVarSub = input$-1 + ((i-1) * 128);
			makeString(tx$, "\x89\x00\x00\x03%s%s%s\x00\x00\x00\x00\x03\x03\x03\x03\x03", objectID$, chr(high(stateVarSub)), chr(low(stateVarSub)));  //ON OFF
			processlogic();
			stateVarSub = stateVarSub + 16384;
			makeString(tx$, "\x8E\x00\x00\x03%s%s%s\x00\x00\x00\x00\x03\x03\x03\x03\x03", objectID$, chr(high(stateVarSub)), chr(low(stateVarSub))); //GAIN
	    processlogic();
			delay(5);
		}
		subscribe = 1;
	}
}  

RELEASE subscribe$
{
	stateVarSub = input$-1;
	for (i = 1 to iMAXOUTPUT)
	{   
		stateVarSub = input$-1 + ((i-1) * 128);
		makeString(tx$, "\x8A\x00\x00\x03%s%s%s\x00\x00\x00\x00\x03\x03\x03\x03\x03", objectID$, chr(high(stateVarSub)), chr(low(stateVarSub)));  //ON OFF
		processlogic();
		stateVarSub = stateVarSub + 16384;
		makeString(tx$, "\x8F\x00\x00\x03%s%s%s\x00\x00\x00\x00\x03\x03\x03\x03\x03", objectID$, chr(high(stateVarSub)), chr(low(stateVarSub))); //GAIN
		processlogic();	
		delay(5);
	}
	subscribe = 0;
}  

CHANGE rx$
{
	if (xOK)
	{	
		xOK = 0;
		while (len(rx$))
		{         
			if(find("\x03\x03\x03\x03\x03", rx$))
			{ 
				tempString = remove("\x03\x03\x03\x03\x03", rx$);
				if ((mid(tempString, 6, 3) = "\x00\x00\x00") || (mid(tempString, 6, 3) = objectID$))
				{                               
					stateVarReceive = (byte(tempString, 9) * 256) + byte(tempString, 10);
					if ((stateVarReceive MOD 128) = input$-1)
					{
						if (stateVarReceive < 16384)
						{                     
							if (byte(tempString, 14))
							{
								onOff_fb$[(stateVarReceive / 128) + 49] = 0;														
								onOff_fb$[(stateVarReceive / 128) + 1] = 1;							
							}
							else
							{
								onOff_fb$[(stateVarReceive / 128) + 1] = 0;							
								onOff_fb$[(stateVarReceive / 128) + 49] = 1;							
							}
						}   
						else
						{ 
							stateVarReceive = ((stateVarReceive - 16384) / 128) + 1;
							volumeOutput[x] = volumePercentToI(mid(tempString, 11, 4));
							gain_fb$[stateVarReceive] = volumeOutput[x];						
						}
					}
				}
				clearbuffer(tempString);					
			}    	
			
		}
		xOK = 1;
	}	
}
/*******************************************************************************************
  Main()
  Uncomment and place one-time startup code here
  (This code will get called when the system starts up)
*******************************************************************************************/
Function Main()
{
	xOK = 1;
	subscribe = 0;
}
